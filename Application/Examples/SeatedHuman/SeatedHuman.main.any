/** The Seated Human is a family of models resulting from a
research project involving the furniture industry. This
model is a human sitting in a generic chair where the seat,
backrest, arm rests, foot rest and head rest can be adjusted
and their influence on the forces in the human body can be
investigated.
Notice that the contact conditions are conditional with
respect to the distance between the body and the surface
in question so that they are only active when the contact
pair is close to each other.
The contact between the human body and the chair is by means
of contact elements that can only provide compression and
friction but no tension. The available friction is proportional
to the normal force and the user can supply a friction 
coefficient for each surface such that the effect of different 
surface fabrics can be investigated.*/
Main = {
  
  /**Execute this operation to run the model in the intended operation sequence.  
  It is also possible to run operations seperately, by manual selections in the operation tree*/
  AnyOperationSequence RunApplication = {

    /**This operation calibrates the muscles in the model if these are of the type AnyMuscleModel3E.
    This will just be an empty operation if the model is using a muscle type that does not require calibration.*/
    AnyOperation &CalibrationAnal = Main.HumanModel.Calibration.CalibrationSequence;  

    ///This operation is the inverse dynamic analysis
    AnyOperation &InvAnal=Main.Study.InverseDynamics;
  
  };
  
  /// Driver Positions - in degrees
  AnyFolder DrvPos = {
    AnyVar PelvisSeatLinXPos = 0.05;      ///< Position of the pelvis on the seat
    AnyVar SeatGlobalLinYPos = 0;         ///< Seat Height
    AnyVar SeatGlobalZRot = -30;            ///< Forward seat inclination
    AnyVar BackRestHeadResRotZPos = 0;    ///< Rotation of the head rest wrt the back rest
    AnyVar GlobalLegRestRotZPos = 75;     ///< Inclination of the leg rest
    AnyVar GlobalBackRestRotZPos = 10;    ///< Back rest inclination
    AnyVar LegRestFootRestLinYPos = 0.1; ///< Position of the footrest along the leg rest
  }; // DrvPos
  
  /// Driver Velocities - angles are in degrees per second
  AnyFolder DrvVel = {
    AnyVar PelvisSeatLinXVel = 0;         ///< Movement of the pelvis on the seat
    AnyVar SeatGlobalLinYVel = 0;         ///< Movement of the seat Height
    AnyVar SeatGlobalZRotVel = 0;         ///< Movement of seat inclination
    AnyVar GlobalBackRestRotZVel = 30;     ///< Back rest inclination velocity
    AnyVar GlobalLegRestRotZVel = 0;      ///< Leg rest inclination velocity
    AnyVar LegRestFootRestLinYVel = 0;    ///< Movement of the footrest along the leg rest
  }; // DrvVel
  
  // --------------------------------------------------------
  // Support settings 
  // --------------------------------------------------------
  /// Settings for the support elements, friction coefficients and
  /// strength of the support contact elements. If the strength 
  /// gets too low it means that the body will start using muscle
  /// force to avoid laying too much 
  AnyFolder SupportSettings = {
    
    //Friction coefficients between the human and the chair
    AnyVar StaticFrictionSeat = 0.5;
    AnyVar StaticFrictionBackrest = 0.5;
    AnyVar StaticFrictionFootrest = 0.5; 
    AnyVar StaticFrictionArmrest = 0.2; 
    
    //Strenght of the support muscles between the human and the chair
    AnyVar SeatSupportStrength=10000;
    AnyVar BackrestSupportStrength=10000;
    AnyVar FootrestSupportStrength=10000;
    AnyVar ArmrestSupportStrength=10000;
  };
  
  // ----------------------------------------------------------
  // Draw settings
  // ----------------------------------------------------------
  #include "DrawSettings.any"
  
  AnyFolder HumanModel = {
    
    AnyFolder &Mannequin=.Model.Mannequin;
    
    // The NoMuscles version is useful for playing around with the kinematics
    //#include  "..\..\..\Body\AAUHuman\BodyModels\FullBodyModel\BodyModel_NoMuscles.any"    
    
    // This model uses the simple one-parameter muscles 
    #include  "..\..\..\Body\AAUHuman\BodyModels\FullBodyModel\BodyModel.any"    
    
    AnyFolder StrengthParameters={
      AnyVar SpecificMuscleTensionSpine= 90; //N/cm^2
      AnyVar StrengthIndexLeg= 1; 
      AnyVar SpecificMuscleTensionShoulderArm= 90; //N/cm^2
    };
    
    // Use the standard size human model.
    //#include "..\..\..\Body\AAUHuman\Scaling\ScalingStandard.any"
    //#include "..\..\..\Body\AAUHuman\Scaling\ScalingUniform.any"
    //#include "..\..\..\Body\AAUHuman\Scaling\ScalingLengthMass.any"
    //#include "..\..\..\Body\AAUHuman\Scaling\ScalingLengthMassFat.any"
    // Scaling laws using external measures
    //#include "..\..\..\Body\AAUHuman\Scaling\ScalingUniformExt.any"
    #include "..\..\..\Body\AAUHuman\Scaling\ScalingLengthMassExt.any"
    //#include "..\..\..\Body\AAUHuman\Scaling\ScalingLengthMassFatExt.any"
    //#include "DrawExternalMeasures.any"
    Scaling = {
       //#include "..\..\..\Body\AAUHuman\Scaling\AnyFamily\AnyMan.any"
       //#include "..\..\..\Body\AAUHuman\Scaling\AnyFamily\AnyManUniform.any"
       #include "..\..\..\Body\AAUHuman\Scaling\AnyFamily\AnyManExternal.any"
       //#include "..\..\..\Body\AAUHuman\Scaling\AnyFamily\AnyManExtPercentile.any"
       //#include "..\..\..\Body\AAUHuman\Scaling\AnyFamily\AnyWomanExtPercentile.any"
    };

  };
  
  // ------------------------------------------------------------
  // Model
  // ------------------------------------------------------------
  
  /// This folder collects all the objects in the model.
  AnyFolder Model = {
    AnyFolder &HumanModel=.HumanModel.BodyModel;
    #include "Mannequin.any"
    #include "Environment.any"
    
    /// Objects connecting the human to the chair
    AnyFolder ModelEnvironmentConnection = {
      
      #include "ConnectionSegments.any"
      #include "JointsAndDrivers.any"
      
      AnyFolder &RefPM=Main.Model.EnvironmentModel;
      #include "InitialPositionsEnvironment.any" 
      #include "Support.any" 
    }; // ModelEnvironmentConnection
  }; // Model
  
  // --------------------------------------------------------------
  // Study
  // --------------------------------------------------------------

  AnyBodyStudy Study = {
    AnyFolder &Model = .Model;
   
    tEnd = 1;
    Gravity = {0.0, -9.81, 0.0};
    nStep = 5;
    
    //The study of sum of mascular force
    //Right spine muscles
    AnySearchFun MultifidiRightList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineRight.Multifidi.*.Fm";};
    AnySearchFun ErectorSpinaeRightList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineRight.ErectorSpinae.*.Fm";};
    AnySearchFun PsoasMajorRightList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineRight.PsoasMajor.*.Fm";};
    AnySearchFun QuadratusLumborumRightList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineRight.QuadratusLumborum.*.Fm";};
    AnySearchFun SemispinalisRigthList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineRight.SemispinalisRigth.*.Fm";};
    AnySearchFun ThoracicMultifidiRigthList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineRight.ThoracicMultifidiRigth.*.Fm";};
    //Left spine muscles
    AnySearchFun MultifidiLeftList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineLeft.Multifidi.*.Fm";};
    AnySearchFun ErectorSpinaeLeftList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineLeft.ErectorSpinae.*.Fm";};
    AnySearchFun PsoasMajorLeftList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineLeft.PsoasMajor.*.Fm";};
    AnySearchFun QuadratusLumborumLeftList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineLeft.QuadratusLumborum.*.Fm";};
    AnySearchFun SemispinalisLeftList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineLeft.SemispinalisLeft.*.Fm";};
    AnySearchFun ThoracicMultifidiLeftList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpineLeft.ThoracicMultifidiLeft.*.Fm";};
    //middle
    AnySearchFun SpinalisList = {Search = "Main.HumanModel.BodyModel.Trunk.MusclesSpine.Spinalis.*.Fm";};

    //AnyVar MultifidiMaxForce = max(MultifidiList());
    //AnyVar MultifidiMeanForce = mean(MultifidiList());
    //Right spine forces
    AnyVar MultifidiRightSumForce = sum(MultifidiRightList());
    AnyVar ErectorSpinaeRightSumForce = sum(ErectorSpinaeRightList());
    AnyVar PsoasMajorRightSumForce = sum(PsoasMajorRightList());
    AnyVar QuadratusLumborumRightSumForce = sum(QuadratusLumborumRightList());
    AnyVar SemispinalisRigthSumForce = sum(SemispinalisRigthList());
    AnyVar ThoracicMultifidiRigthSumForce = sum(ThoracicMultifidiRigthList());
    //left spine forces
    AnyVar MultifidiLeftSumForce = sum(MultifidiLeftList());
    AnyVar ErectorSpinaeLeftSumForce = sum(ErectorSpinaeLeftList());
    AnyVar PsoasMajorLeftSumForce = sum(PsoasMajorLeftList());
    AnyVar QuadratusLumborumLeftSumForce = sum(QuadratusLumborumLeftList());
    AnyVar SemispinalisLeftSumForce = sum(SemispinalisLeftList());
    AnyVar ThoracicMultifidiLeftSumForce = sum(ThoracicMultifidiLeftList());
    //middle forces
    AnyVar SpinalisSumForce = sum(SpinalisList());
    
    //Size of total force in the L5 Sacrum joint
    AnyVar L5SacrumReac = (Main.Model.HumanModel.Trunk.JointsLumbar.L5SacrumJnt.Constraints.Reaction.Fout[0]^2+
    Main.Model.HumanModel.Trunk.JointsLumbar.L5SacrumJnt.Constraints.Reaction.Fout[1]^2+
    Main.Model.HumanModel.Trunk.JointsLumbar.L5SacrumJnt.Constraints.Reaction.Fout[2]^2)^0.5;
  }; // Study  

  
};// Main
